; Install program generation script for KevEdit in windows
; Requires Inno Setup

[Setup]
AppName=KevEdit
AppPublisher=Kev Vance and Ryan Phillips
AppPublisherURL=http://github.com/cknave/kevedit
AppSupportURL=https://github.com/cknave/kevedit/issues
AppUpdatesURL=https://github.com/cknave/kevedit/releases
AppVerName=KevEdit @VERSION@
ArchitecturesAllowed=x64
ArchitecturesInstallIn64BitMode=x64
ChangesAssociations=yes
DefaultDirName={autopf}\KevEdit
DefaultGroupName=Games\ZZT
InfoBeforeFile=Z:\work\kevedit\README.md
OutputDir=Z:\dist
PrivilegesRequired=lowest
PrivilegesRequiredOverridesAllowed=dialog

; Cosmetics
WindowVisible=no
WizardImageFile=Z:\platform\windows\install-big.bmp
WizardSmallImageFile=Z:\platform\windows\install-small.bmp
WizardStyle=modern

[Files]
Source: "Z:\work\AUTHORS.txt";  DestDir: "{app}"; Flags: ignoreversion
Source: "Z:\work\ChangeLog.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "Z:\work\COPYING.txt";  DestDir: "{app}"; Flags: ignoreversion
Source: "Z:\work\kevedit\src\kevedit\kevedit.exe";  DestDir: "{app}"; Flags: ignoreversion
Source: "Z:\work\kevedit\docs\kevedit.zml";  DestDir: "{app}"; Flags: ignoreversion
Source: "Z:\work\kevedit\README.md";   DestDir: "{app}"; Flags: ignoreversion
Source: "Z:\work\kevedit\legal.md";   DestDir: "{app}"; Flags: ignoreversion
Source: "Z:\work\sdl\README-SDL.txt"; Destdir: "{app}"; Flags: ignoreversion
Source: "Z:\work\sdl\SDL2.dll";      Destdir: "{app}"; Flags: ignoreversion

[Icons]
Name: "{group}\KevEdit"; Filename: "{app}\kevedit.exe"; WorkingDir: "{userdocs}"
Name: "{group}\KevEdit Readme";  Filename: "{app}\README.md"
Name: "{group}\Uninstall KevEdit"; Filename: "{uninstallexe}"

[Run]
Filename: "{app}\kevedit.exe"; Description: "Launch kevedit"; WorkingDir: "{userdocs}"; Flags: nowait postinstall skipifsilent

[Registry]
; Associate KevEdit with ZZT worlds
Root: HKA; Subkey: "Software\Classes\.zzt\OpenWithProgids"; ValueType: string; ValueName: "KevEdit.zzt"; ValueData: ""; Flags: uninsdeletevalue
Root: HKA; Subkey: "Software\Classes\.sav\OpenWithProgids"; ValueType: string; ValueName: "KevEdit.sav"; ValueData: ""; Flags: uninsdeletevalue

Root: HKA; Subkey: "Software\Classes\KevEdit.zzt"; ValueType: string; ValueName: ""; ValueData: "ZZT World"; Flags: uninsdeletekey
Root: HKA; Subkey: "Software\Classes\KevEdit.sav"; ValueType: string; ValueName: ""; ValueData: "ZZT Save"; Flags: uninsdeletekey

Root: HKA; Subkey: "Software\Classes\KevEdit.zzt\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\kevedit.exe,0"
Root: HKA; Subkey: "Software\Classes\KevEdit.sav\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\kevedit.exe,0"

Root: HKA; Subkey: "Software\Classes\KevEdit.zzt\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\kevedit.exe"" ""%1"""
Root: HKA; Subkey: "Software\Classes\KevEdit.sav\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\kevedit.exe"" ""%1"""

Root: HKA; Subkey: "Software\Classes\Applications\kevedit.exe\SupportedTypes"; ValueType: string; ValueName: ".zzt"; ValueData: ""
Root: HKA; Subkey: "Software\Classes\Applications\kevedit.exe\SupportedTypes"; ValueType: string; ValueName: ".sav"; ValueData: ""
